group = 'net.transferproxy'
version = '1.1.0'

buildscript {
    repositories {
        mavenCentral()
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'jvm-test-suite'
    apply plugin: 'maven-publish'

    group = 'net.transferproxy'
    version = rootProject.version

    repositories {
        mavenCentral()
    }

    dependencies {
        // Logging
        api 'org.tinylog:slf4j-tinylog:2.7.0'
        api 'org.tinylog:tinylog-impl:2.7.0'

        // Networking
        api 'io.netty:netty-all:4.1.112.Final'

        // Annotations
        api 'org.jetbrains:annotations:24.1.0'
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
            vendor = JvmVendorSpec.ADOPTIUM
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

    def applyTestDependencies = { config ->
        config.implementation config.platform('org.junit:junit-bom:5.11.3')
        config.implementation 'org.junit.jupiter:junit-jupiter'
        config.implementation 'org.mockito:mockito-core:5.14.2'
    }

    plugins.withType(JvmTestSuitePlugin).tap {
        configureEach {
            testing {
                suites.configureEach {
                    applyTestDependencies(dependencies)
                }
            }
        }
    }

    test {
        useJUnitPlatform()
    }
}