buildscript() {
    repositories {
        maven {
            url 'https://jitpack.io'
        }
    }
    dependencies {
        classpath 'com.github.YvanMazy.MinecraftRemapperGradle:MinecraftRemapperGradle:a2558bb063'
    }
}

plugins {
    id 'com.gradleup.shadow' version '8.3.0'
}

apply plugin: 'be.yvanmazy.minecraftremapper.gradle'

dependencies {
    // Project API
    api project(":api")

    // Testing
    testImplementation project(path: ':api', configuration: 'testClasses')
}

minecraftRemapper {
    version = "1.21.11"
    directionType = 'CLIENT'
    includeRemappedJarDependency = true
    includeLibrariesDependency = true
    includeRawJarDependency = true
    remappedJarDependenciesConfigurations = ["integrationTestCompileOnly"]
    librariesDependenciesConfigurations = ["integrationTestCompileOnly"]
    rawJarDependenciesConfigurations = ["integrationTestRuntimeOnly"]
}

shadowJar {
    manifest {
        attributes 'Main-Class': 'net.transferproxy.main.Main'
    }
    archiveClassifier.set('')
}

tasks.register('copyJar', Copy) {
    onlyIf { !gradle.startParameter.taskNames.contains('assemble') }
    dependsOn(tasks.shadowJar)

    from tasks.shadowJar.archiveFile
    into project.parent.layout.buildDirectory
    rename { "${project.parent.name}-${project.parent.version}.jar" }
}

assemble.dependsOn shadowJar

testing {
    suites.register('integrationTest', JvmTestSuite) {
        targets.configureEach {

            repositories {
                maven { url 'https://jitpack.io' }
            }

            dependencies {
                implementation 'com.github.YvanMazy:RemotedMinecraft:7f570a1787'
                implementation 'org.junit.platform:junit-platform-launcher:1.11.3'
                implementation project()
            }
        }
    }
}

def integrationJavaToolchain = {
    languageVersion = JavaLanguageVersion.of(21)
    vendor = java.toolchain.vendor
}

integrationTest {
    systemProperty 'library.cache.dir', gradle.gradleUserHomeDir.toPath().resolve('caches').resolve('TransferProxy').resolve('data').toString();
    systemProperty 'tinylog.configuration', 'test-tinylog.properties'
    systemProperty 'minecraft.version', minecraftRemapper.version
    javaLauncher.set(javaToolchains.launcherFor(integrationJavaToolchain))
}

tasks.compileIntegrationTestJava {
    javaCompiler.set(javaToolchains.compilerFor(integrationJavaToolchain))
}